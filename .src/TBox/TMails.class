' Gambas class file

Export

Inherits TBox

'******************************************************************************************************
'Declaracion de nuevos eventos
'******************************************************************************************************

'' ## Descripcion ##
'' 
'' Evento que se dispara cuando se produce un error
'' en el ingreso de datos.
Event Error()


'******************************************************************************************************
'Instancias Privadas
'******************************************************************************************************
Private MyMsj As New PopuMsj ''instancia de la clase que muestra mensajes

'******************************************************************************************************
'Constantes especiales para Controles
'******************************************************************************************************
Public Const _Group As String = "Osuna" ''para que se muestre en esta solapa de herramientas
Public Const _DrawWith As String = "TextBox" ''para que se dibuje como un textbox

'******************************************************************************************************
'Propiedades
'******************************************************************************************************

'' ## Descripcion ##
'' 
'' Valor booleano que representa si es o no un mail la cadena contenida
Property Read IsMail As Boolean
Private bIsMail As Boolean

'' ## Descripcion ##
'' 
'' Determina si se muestra o no un mensaje emergente
'' al confirmar que el datos es incorrecto
Property PopMsjMailError As Boolean
Private bPopMsjMailError As Boolean

'' ## Descripcion ##
'' 
'' Texto del mensaje de error
Property TextMsjError As String
Private sTextMsjError As String


''y estas son las propiedades que deben mostrarse en el cuadro de dialogo 
''de propiedades en el ide, el asterisco inicial indica que se deben de 
''agregar a las ya existente por la herencia
Public Const _Properties As String = "*,TextMsjError,PopMsjMailError,-Translate,-PopupMaxLength,-MaxLength,-Drop,-Password"

'******************************************************************************************************
'Codigo publico
'******************************************************************************************************
Public Sub _new()
'------------------------Se mira 
    Dim oChusma As Observer
    oChusma = New Observer(Me) As "TMails" 
    
End Sub

Public Sub TMails_LostFocus()
  
  If CStr.IsMail(Me.Text) Then
    bIsMail = True
  Else
    bIsMail = False
    If bPopMsjMailError Then MyMsj.PopouMsj(sTextMsjError, Me)    
    Raise Error()    
  Endif
  
End

'******************************************************************************************************
'Codigo manejo de propiedades
'******************************************************************************************************

Private Function IsMail_Read() As Boolean

  Return bIsMail  

End

Private Function PopMsjMailError_Read() As Boolean

  Return bPopMsjMailError

End
  
Private Sub PopMsjMailError_Write(Value As Boolean)

  bPopMsjMailError = Value

End

Private Function TextMsjError_Read() As String

  Return sTextMsjError  

End

Private Sub TextMsjError_Write(Value As String)

  sTextMsjError = Value

End
